#!ipxe

#PARAMS
#set uri-params  uuid=${uuid}
#set uri-params  ${uri-params}&serial=${serial}
#set uri-params  ${uri-params}&manufacturer=${manufacturer}
#set uri-params  ${uri-params}&product=${product}
#set uri-params  ${uri-params}&mac=${mac}

#Default menu option: exit.
isset ${menu-default} || set menu-default smartos

:start
menu **** ${manufacturer} - ${product} ****
item --gap UUID: ${uuid}
item --gap Serial: ${serial} ${hostname}
item
item --gap Operating systems
item --key s smartos SmartOS
item --key u ubuntu  Ubuntu 14
item --key v vmware  VMWare ESXi
item
item --gap Others
item hdt             Hardware Detection Tool
item memtest         Memtest86+
item systemr         SystemRescueCD
item pmagic          PartedMagic
item
item shell           iPXE shell
item config          iPXE settings
item reboot          Reboot computer
item exit            Exit iPXE and continue BIOS boot

choose --timeout 3000 --default ${menu-default} selected
goto ${selected}

:shell
echo Type exit to get the back to the menu
shell
goto start

:reboot
reboot

:config
config
goto start

:exit
exit

#### OS's ####
:failed
echo Booting failed, dropping to shell
goto shell

:smartos
set base-url ${boot-url}/images/smartos/platform/i86pc
kernel ${base-url}/kernel/amd64/unix
module ${base-url}/amd64/boot_archive
#imgargs unix -B smartos=true,standalone=true,noimport=true
imgargs unix -B smartos=true
boot || goto failed
goto start

:ubuntu
set base-url ${boot-url}/images/ubuntu
initrd ${base-url}/initrd.gz
chain ${base-url}/linux

#chain ${base-url}/linux url=http://preseed.panticz.de/preseed/ubuntu-minimal.seed auto=true netcfg/choose_interface=${net0/mac} locale=en_US.UTF-8 priority=critical biosdevname=0


:vmware
echo VMWaresssss
echo Booting VMware ESXi from iSCSI for ${initiator-iqn}
set root-path ${base-iscsi}:${hostname}.boot.vmware
sanboot ${root-path} || goto failed



#### Tools ####

:hdt
echo HDT
sanboot ${boot-url}/images/hdt/hdt-0.5.2.iso || goto failed
boot

:memtest
echo Memtest
sanboot ${boot-url}/images/memtest86/Memtest86-6.0.0.iso || goto failed

:systemr
echo Systemrecue
sanboot ${boot-url}/images/systemrescue/systemrescuecd-x86-4.5.2.iso
#kernel ${boot-url}/memdisk
#initrd ${boot-url}/systemrescue/systemrescuecd-x86-4.5.2.iso
#boot || goto failed

:pmagic
initrd ${boot-url}/images/pmagic/pmagic_2013_02_28.iso
chain ${boot-url}/memdisk iso || goto failed
